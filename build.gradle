plugins {
    id 'maven-publish'
    id 'signing'
    id 'java-library'
    id "com.github.sherter.google-java-format" version "0.9"
    id 'com.github.ksoichiro.console.reporter' version '0.6.2'
    id 'io.codearte.nexus-staging' version '0.21.2'
}

group 'io.leonard'
version '0.0.1'

sourceCompatibility = 11

repositories {
    jcenter()
    mavenCentral()
}

git {
    implementation 'https://github.com/simonpoole/OpeningHoursParser.git', {
        tag '0.22.0'
    }
}

compileJava {
    options.compilerArgs << '-parameters'
}

ext {
    junitVersion = '5.6.2'
}

dependencies {
    testImplementation  group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitVersion
    testRuntimeOnly  group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
}

googleJavaFormat {
    options style: 'AOSP'
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
}

//-- begin maven-publish.publish  spell --
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}
task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'OpenStreetMap Opening Hours evaluator'
                description = 'Evaluator for OpenStreetMap-formatted opening hours'
                url = 'http://javers.org'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'leonardehrenfried'
                        name = 'Leonard Ehrenfried'
                        email = 'mail@leonard.io'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/leonardehrenfried/opening-hours-evaluator.git'
                    developerConnection = 'scm:git:https://github.com/leonardehrenfried/opening-hours-parser.git'
                    url = 'https://github.com/leonardehrenfried/opening-hours-parser'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username project.findProperty('ossrhUser') ?: 'secured'
                password project.findProperty('ossrhPassword') ?: 'secured'
            }
        }
    }
}

signing {
    sign publishing.publications
}